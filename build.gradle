buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.0'
		classpath 'com.github.ben-manes:gradle-versions-plugin:0.11.3'
        classpath 'io.fabric.tools:gradle:1.20.1'
    }
}

apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://jitpack.io' }
    maven { url 'http://maven.arcao.com' }
}

dependencies {
    // from Android local maven repository
    compile 'com.android.support:support-v4:23.1.0'
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.android.support:recyclerview-v7:23.1.0'
    compile 'com.android.support:cardview-v7:23.1.0'
    compile 'com.android.support:design:23.1.0'

    // from maven.arcao.com repository
    compile('com.arcao:geocaching-api:1.6.2') {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        // Not working in Android, slf4j-timber used instead
    }
    compile 'menion:locus-api-android:1.35.39'

    // from JCenter repository
    compile 'com.arcao:slf4j-timber:2.1'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'org.apache.commons:commons-collections4:4.0'
    compile 'commons-io:commons-io:2.4'
    compile('org.scribe:scribe:1.3.7') {
        exclude group: 'commons-codec', module: 'commons-codec' // already in Android
    }
    compile 'com.jakewharton:butterknife:7.0.1'
    compile('com.afollestad.material-dialogs:core:0.8.5.0@aar') {
        transitive = true
    }
    compile 'com.crashlytics.sdk.android:crashlytics:2.5.2'
    compile 'com.squareup.okhttp:okhttp:2.5.0'
}

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm:ss.SSSZ", TimeZone.getTimeZone("UTC"))
def gitVersionCode = 'git rev-list --count HEAD'.execute([], project.rootDir).text.trim().toInteger();

def isTravis = "true".equals(System.getenv("TRAVIS"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

// set Geocaching API staging key and secret if production key and secret is not set
// Note: Staging server is slow and not for production use!!!!
if (!project.hasProperty('geocachingApiKey')) {
    ext.geocachingApiKey = '9C7552E1-3C04-4D04-A395-230D8931E494'
}
if (!project.hasProperty('geocachingApiSecret')) {
    ext.geocachingApiSecret = 'DA7CC147-7B5B-4423-BCB4-D0C03E2BF685'
}
if (!project.hasProperty('geocachingApiStaging')) {
    ext.geocachingApiStaging = true
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries preDexEnabled && !isTravis
    }

    defaultConfig {
        versionCode gitVersionCode
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
        buildConfigField "String", "GEOCACHING_API_KEY", "\"${geocachingApiKey}\""
        buildConfigField "String", "GEOCACHING_API_SECRET", "\"${geocachingApiSecret}\""
        buildConfigField "boolean", "GEOCACHING_API_STAGING", "${geocachingApiStaging}"
    }

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            keyAlias = "geocaching4locus"
        }
    }

    buildTypes {
        debug {
            versionNameSuffix '-dev'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            // shrinkResources true // too aggressive
            proguardFile getDefaultProguardFile('proguard-android-optimize.txt')
            proguardFile 'proguard.cfg'
        }
    }

    packagingOptions {
        // removed because of conflict in creating package
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        // unused files, removed to minimize APK file
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'org/apache/http/version.properties'
        exclude 'templates/release-notes.vm'
        exclude 'log4j.xml'
    }
    lintOptions { abortOnError false }
}

if (project.hasProperty('storeFile') &&
        project.hasProperty('storePassword') &&
        project.hasProperty('keyPassword')) {
    android.signingConfigs.release.storeFile = file(storeFile)
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    android.buildTypes.release.signingConfig = android.signingConfigs.debug
}