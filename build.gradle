buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.+'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.9.+'
    }
}
apply plugin: 'android-sdk-manager'
apply plugin: 'android'

repositories {
    mavenCentral()
    maven { url "http://maven.arcao.com/" }
}

dependencies {
    compile 'com.android.support:support-v4:19.0.+'
    compile ('ch.acra:acra:4.5.0') {
      exclude group: 'org.json', module: 'json' /* already in Android */
    }
    compile 'org.apache.commons:commons-lang3:3.1'
    compile('com.arcao:geocaching-api:1.5.18') {
      exclude group: 'log4j', module: 'log4j' /* Not working in Android, log4j-over-slf4j used instead */
    }
    compile('net.sf.jtpl:jtpl:2.2') {
      exclude group: 'junit', module: 'junit' /* Not required */ 
    }
    compile 'menion:locus-api-android:1.16.14'
    compile('oauth.signpost:signpost-commonshttp4:1.2.1.2') {
      exclude group: 'org.apache.httpcomponents', module: 'httpclient' /* already in Android */ 
      exclude group: 'commons-codec', module: 'commons-codec' /* already in Android */
    }
    compile('org.slf4j:log4j-over-slf4j:1.6.4') {
      exclude group: 'org.slf4j', module: 'slf4j-api' /* Not working in Android, slf4j-android used instead */
    }
    compile 'org.slf4j:slf4j-android:1.6.1-RC1'
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.3"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            keyAlias = "geocaching4locus"
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            runProguard true
            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile 'proguard.cfg'
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        instrumentTest.setRoot('tests')
    }

    packagingOptions {
        // removed because of conflict in creating package
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        // unused files, removed to minimize APK file
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'org/apache/http/version.properties'
        exclude 'templates/release-notes.vm'
    }

    // use old PNGCruncher, the new one cause crash on 2.x devices
    aaptOptions {
        useAaptPngCruncher = true
    }
}

if (project.hasProperty('storeFile') &&
        project.hasProperty('storePassword') &&
        project.hasProperty('keyPassword')) {
    android.signingConfigs.release.storeFile = file(storeFile)
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    android.buildTypes.release.signingConfig = android.signingConfigs.debug
}